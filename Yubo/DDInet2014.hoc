load_file("nrngui.hoc")
load_file("mitral.hoc")
load_file("gc.hoc")
cvode.active(1)

Vrest = -65
dt = 1
celsius=35
tstop=1500

mtnum=10
gcnum=30
gcconnection=5  //.the number of mt-gc connections of a GC cell
mtconnection=gcnum*gcconnection/mtnum

objref nconp[mtnum], net, netp[mtnum], g, b, synp, nil, stim, apc
objref mt[mtnum], gc[gcnum], train, outfile, coeff[10], index[14],table
objref nc[gcnum*gcconnection*3]

strdef extension
extension = "dat" //.dat extension for data files

outfile = new File()

table = new Vector()

for i=0,mtnum-1 {
mt[i] = new Mitral()
}

for i=0,9 {
coeff[i] = new Vector()
}

for i=0,gcnum-1 {
gc[i] = new GC()
}

for i=0,13 {
index[i] = new Vector()
}


//// record mt voltage trace and time into vectors	
objref rect,savdatat,savdatamt,recv,tempmatrix,recvtemp,recvtemp2
strdef mtfilename, mtbasename
mtbasename = "mt_O"

recv=new List()
recvtemp2=new Vector()
tempmatrix=new Matrix()
rect = new Vector()
savdatat = new File()
savdatamt=new File()

rect.record(&t)

/////tempmatrix includes time vector
tempmatrix.resize(50000,mtnum+1)

for i=0,mtnum-1{
  recvtemp=new Vector()
  recvtemp.record(&mt[i].soma.v(0.5))
  recv.append(recvtemp)
}

//// record gc voltage trace and time into vectors	
objref savdatatgc,savdatagc,recvgc,tempmatrixgc,recvtempgc,recvtempgc2
strdef gcfilename, gcbasename
gcbasename = "gc_O"

recvgc=new List()
recvtempgc2=new Vector()
tempmatrixgc=new Matrix()
savdatagc = new File()
savdatatgc=new File()


////////tempmatrixgc includes time vector

tempmatrixgc.resize(50000,gcnum+1)


for i=0,gcnum-1{
  recvtempgc=new Vector()
  recvtempgc.record(&gc[i].somagc.v(0.5))
  recvgc.append(recvtempgc)
}


///////odor sensitivity of MCs
outfile.ropen("CoeffTable_9OD_10MC.txt")
for i=0,9{
for j=0,8 {
coeff[i].append(outfile.scanvar())
}
coeff[i].printf()
}
outfile.close()

////get index number from index-table.
outfile.ropen("index-table.txt")
for j=0,13 {
index[0].append(outfile.scanvar())
}
index[0].printf()
outfile.close()

cx=1

weight=.1
amp=.03
rel=0.2
inh=13
synstr=2.3
nmdafactor=0.0035
frac=1

b = new VBox()
b.intercept(1)
g = new Graph()
g.size(0,tstop,-70,0)
g.xaxis(1)
g.addvar("mt[0].soma.v(0.5)",1,1,0.7,1,2)
g.addvar("mt[1].soma.v(0.5)",2,1,0.7,0.99,2)
g.addvar("mt[2].soma.v(0.5)",3,1,0.7,0.98,2)
g.exec_menu("10% Zoom out")
xpanel("",1)
xbutton("run", "run()")
xvalue("odor","cx")
xpanel()
b.intercept(0)
b.map()

///////////NetStim: presynaptic stimulus on MC, spike train. 
for i=0, mtnum-1 {
access mt[i].soma
	distance()
	netp[i] = new NetStim(0)
      ////trigger spike (first spike), then the average input current to MC.Try change the numbers of spikes or the interval of each spike.

	netp[i].number=1
	netp[i].interval=500
	netp[i].noise=0
      /////starting time 2ms.
	netp[i].start=2
      //////connect netp(presynaptic) to mt synodor object(postsynaptic), but weight?
	nconp[i]= new NetCon(netp[i],mt[i].synodor,0.5,0,weight*1.e-3) 
}

////////////////// circuit definition  

///// gc <-> mt
w0=1 
w1=0.8
w2=0.4

nccount=0

 for i=0,2 {
   
    for k=0,mtnum-1{
    
     ////right side connections of gc[k]
      for j=0,1 {
       nccount=nccount+1
       gc[k+i*mtnum].dend[j*2+2]   nc[nccount] = new NetCon(&v(1),mt[index[0].x[k+j+3]].igp[0][(j+1)+i*2],-40,1,w1*inh*1e-3)

       nccount=nccount+1
       mt[index[0].x[k+j+3]].secden[0]  nc[nccount]= new NetCon(&v(0),gc[k+i*mtnum].synmt[j+1],-40,1,w1*synstr*nmdafactor)

       nccount=nccount+1
       mt[index[0].x[k+j+3]].secden[0]  nc[nccount]= new NetCon(&v(0),gc[k+i*mtnum].sampa[j+1],-40,1,w1*synstr*1e-3)

                 }

     
     /////left side connections of gc[k] 
       for j=0,1 {
       nccount=nccount+1
       gc[k+i*mtnum].dend[j*2+6]   nc[nccount] = new NetCon(&v(1),mt[index[0].x[k+j+1]].igp[1][(j+1)+i*2],-40,1,w2*inh*1e-3)

       nccount=nccount+1
       mt[index[0].x[k+j+1]].secden[1]  nc[nccount]= new NetCon(&v(0),gc[k+i*mtnum].synmt[j+3],-40,1,w2*synstr*nmdafactor)

       nccount=nccount+1
       mt[index[0].x[k+j+1]].secden[1]  nc[nccount]= new NetCon(&v(0),gc[k+i*mtnum].sampa[j+3],-40,1,w2*synstr*1e-3) 

                }
        
  
              }
           }
    
    ////middle connections of gc[k]
     for i=0,1 {

      for k=0,mtnum-1 {
       nccount=nccount+1
       gc[k+mtnum*i].dend[0]   nc[nccount] = new NetCon(&v(1),mt[index[0].x[k+3]].igp[i][0],-40,1,w0*inh*1e-3)

       nccount=nccount+1
       mt[index[0].x[j+3]].secden[i]  nc[nccount]= new NetCon(&v(0),gc[k+mtnum*i].synmt[0],-40,1,w0*synstr*nmdafactor)

       nccount=nccount+1
       mt[index[0].x[j+3]].secden[i]  nc[nccount]= new NetCon(&v(0),gc[k+mtnum*i].sampa[0],-40,1,w0*synstr*1e-3)

                 }
              }
     /////gc[20]-gc[30] don't have middle connections, or they can all be connected with igp[0 or 1][7] of all MTs.


////////////////// end circuit definition


proc init() {
	t=0
	finitialize(Vrest)
        fcurrent()
        forall {
		v=Vrest
		if (ismembrane("nax")) {e_pas=v+(ina+ik)/g_pas
		} else {
		e_pas=v+ik/g_pas
		}
	}
	cvode.re_init()
	g.begin()
	g.plot(t)
}

proc advance() {
	fadvance()
	g.plot(t)
	g.flush()
	doNotify()
}

////// x[i], returns the value for the index i
proc run() {
	for i=0, 9 {
	nconp[i].weight=coeff[i].x[cx-1]*1.e-3
	print i, nconp[i].weight
}
	stdinit()
	continuerun(tstop)

//// Concatenate file name and open file for writing data
	sprint(mtfilename, "%s%d.%s", mtbasename, cx, extension)
	savdatamt.wopen(mtfilename)
	
	//// print titles of columns to file
	savdatamt.printf("mt\n")
	savdatamt.printf("%d\n",rect.size())
	
	//// print data to file

	///////o():return the object at index i

      tempmatrix.setcol(0,rect)
     for i=0,mtnum-1{
      recvtemp2=recv.o(i)      
      tempmatrix.setcol(i+1,recvtemp2) 
              }

     tempmatrix.fprint(savdatamt, " %g")
     savdatamt.close()

//// Concatenate file name and open file for writing data
	sprint(gcfilename, "%s%d.%s", gcbasename, cx, extension)
	savdatagc.wopen(gcfilename)
	
	//// print titles of columns to file
	savdatagc.printf("gc\n")
	savdatagc.printf("%d\n",rect.size())
	
	//// print data to file
	

       tempmatrixgc.setcol(0,rect)

     for i=0,gcnum-1{
      recvtempgc2=recvgc.o(i)
      tempmatrixgc.setcol(i+1,recvtempgc2) 
              }

     tempmatrixgc.fprint(savdatagc, " %g")
     savdatagc.close()

}
